apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group 'br.com.moip'
archivesBaseName = "sdk-java"
version '1.1.0'

description = "Java SDK for Moip APIs integration"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: "org.apache.httpcomponents", name: "httpclient", version: "4.5.3"
    compile group: "org.apache.commons", name: "commons-lang3", version: "3.6"
    compile group: "org.slf4j", name: "slf4j-api", version: "1.7.12"
    compile group: "com.google.code.gson", name: "gson", version: "2.4"
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.5'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: "org.slf4j", name: "slf4j-simple", version: "1.7.12"
    testCompile group: "com.github.rodrigosaito", name: "mockwebserver-player", version: "1.1.0"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getenv('MVN_USERNAME'), password: System.getenv('MVN_PASSWORD'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.getenv('MVN_USERNAME'), password: System.getenv('MVN_PASSWORD'))
            }

            pom.project {
                name 'Moip Java SDK'
                packaging 'jar'
                // the artifactId can be defined here (it's optional)
                description 'Java SDK for Moip APIs integration'
                url 'https://github.com/moip/moip-sdk-java'

                scm {
                    connection 'scm:git:ssh://git@github.com:moip/moip-sdk-java.git'
                    developerConnection 'scm:git:ssh://git@github.com:moip/moip-sdk-java.git'
                    url 'https://github.com/moip/moip-sdk-java'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'matheus.nakaya'
                        name 'Matheus B. Nakaya'
                        email 'matheus.nakaya@moip.com.br'
                    }
                }
            }
        }
    }
}

processResources {
    filesMatching("**/moipJavaSDK.properties") {
        expand([
                version: version
        ])
    }
}
